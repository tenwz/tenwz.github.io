---
layout: post
title: 利用 call/cc 来实现协程
---

### 什么是 call/cc ?

首先引入 context (上下文) 和 escaper (逃逸子)两个概念

context 是一个单参函数.比如

``` scheme
(+ 2 (* 3 4))
```

其中 3 的 context 就是

```scheme
(lambda (hole) (+ 2 (* hole 4))
```

escaper 是一个单参函数，它接受一个函数作为参数，把这个函数转换为 <u>逃逸函数</u> 并返回。

什么是逃逸函数呢？ 
假设 sin 是一个逃逸函数               

```scheme
(+ 1 2 (* 3 (sin 4)))
```

该表达式 只会计算 （sin 4）值，而不用管这个表达式其余的部分。

逃逸函数就是，无论逃逸函数外的环境怎样，表达式只计算逃逸函数的那一部分。

escaper 具有把普通函数转换为逃逸函数的能力，上式可写作

``` scheme
(+ 1 2 (* 3 ((escaper sin) 4)))
;; 这一串表达式 只会计算 (sin 4)
```

---

现在来解释 call/cc 的含义

call/cc 是一个单参函数，它接受一个单参函数作为参数。
它把 (call/cc function) 所在的上下文（别忘了 context 是一个单参函数）做成逃逸形式,传给它接受的单参函数作为参数。。。。太绕了。。。

形式上是这样的

``` scheme
(body1 (call/cc receiver) body2)
;; receiver 是一个单参函数，（call/cc receiver) 的 context 是 
;; (lambda (hole) (body1 hole body2))
;; 它 把这个函数做成逃逸形式 即 （escaper context） 作为参数传给 receiver
(body1 (receiver (escaper (context))) body2)
```

---

举个例子作为练习，进一步理解 call/cc 的作用

``` scheme
(call/cc (lambda (break)
        (for-each (lambda (x) (if (< x 0) (break x)))
                '(99 88 -77 66 55))
        #t))
```

啊。。如果你是新手，你可能不懂 for-each 是什么，这里有一篇文章，可以看一下

[for-each是什么](https://blog.csdn.net/keyboardOTA/article/details/39910043)

现在来分析一下这个代码

``` scheme
这段代码 (call/cc fun) 的 context 是什么 ？
其实就是 (lambda (hole) hole) 即输入什么返回什么
我们把这个函数做为逃逸子，目的是当调用这个函数 立即返回 它的参数，不去管整个表达式
ok 继续看下去，99 < 0 吗 ？不，继续递归，88 < 0 吗？ 不，继续递归，-77 < 0 吗？ 是的 调用（break -77）
break 是什么 ？就是我们上边的那个直接返回自己参数 逃逸函数 
所以 （break -77） 会直接停止整个表达式的计算过程 返回 -77 ！！
```

所以，在这段代码中，我们利用 call/cc 手动实现了一个自己的 break 控制结构

---

再来一个例子

我们可以为 scheme 创造一个 return 语句

``` scheme
(define (foo x)
  (call/cc (lambda(return)
             (return (+ x x)))))
```

---

call/cc 的好处是 我们可以把一段代码的 continuation （它接下来要做的事）显式 的提取出来为我们所用（这句话不太好理解，我哪天再完善一下）

---

下面我们用 call/cc 来实现协程

```scheme
;;continuation栈，保存了等待执行的continuation
(define *queue* '())

(define (empty-queue?)
        (null? *queue*))

(define (enqueue x)
        (set! *queue* (append *queue* (list x))))

(define (dequeue)
        (let ((x (car *queue*)))
              (set! *queue* (cdr *queue*))
         x))
;;启动协程
(define (resume proc)
       (call/cc
         (lambda (k)
           ;;保存当前continuation，执行proc
           (enqueue k)
           (proc))))
;;让出执行权
(define (yield)
     (call/cc
      (lambda (k)
         ;;保存当前continuation，弹出上一次执行的cont并执行
        (enqueue k)
        ((dequeue)))))
;;停止当前协程或者当没有一个协程时停止整个程序,最简单的调度程序
(define (thread-exit)
     (if (empty-queue?)
         (exit)
         ((dequeue))))
```

 这其实就是一个协程的简单实现，context的保存、任务的调度、resume/yield原语……样样俱全。使用起来类似这样，下面这段程序轮流打印字符串：

```scheme
(define (display-str str)
        (lambda()
         (let loop()
              (display str)
              (newline)
              (yield)
              (loop))))

;;;创建两个协程并启动调度器
(resume (display-str "This is AAA"))
(resume (display-str "Hello from BBB"))
(thread-exit)
```

输出：

```scheme
This is AAA
Hello from BBB
This is AAA
Hello from BBB
This is AAA
Hello from BBB
This is AAA
Hello from BBB
…
```

t
