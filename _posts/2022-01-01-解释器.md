---
layout: post
title: A Lisp interpreter written in Lisp










---



```scheme
(define eval-expr
  (lambda (expr env)
    (pmatch expr
            [,x (guard (symbol? x))		
                (env x)]
            [(lambda (,x) ,body)      
             (lambda (arg)
               (eval-expr body (lambda (y)
                                 (if (eq? x y)
                                     arg
                                     (env y)))))]
            [(,rator ,rand)           
             ((eval-expr rator env)
              (eval-expr rand env))])))
```

## 精妙绝伦

## The most beautiful program ever written

这是一个仅仅用了 14 行代码实现的 lisp 解释器，但要想要理解它的精妙之处还真得下一番功夫。

前置知识：[怎样写一个解释器](https://www.yinwang.org/blog-cn/2012/08/01/interpreter)，你应该从这篇文章中掌握「模式匹配 pmatch」和「lexical scoping 词法作用域」的相关知识









---



分析一下这段代码



```scheme
(pmatch expr [,x (guard (symbol? x)) (env x)])
```

如果表达式 expr 是一个 符号 symbol ，那么从环境 env 中寻找该符号所对应的值 value 返回。

(env x)  的作用是从环境中找到 x 所对应的值 value返回。

```scheme
(pmatch expr
        [(lambda (,x) ,body)      
             (lambda (arg)
               (eval-expr body (lambda (y)
                                 (if (eq? x y)
                                     arg
                                     (env y)))))])
```

如果表达式 expr 是一个形如下式的函数定义式

```scheme
(lambda (x) body)
```

那么返回如下函数定义式

```scheme
(lambda (arg)
               (eval-expr body 
                          (lambda (y)
                                 (if (eq? x y)
                                     arg
                                     (env y)))))
```

该函数定义式的函数体（" ( lambda ( arg ) " 之后的内容）是对在下式「环境」中对 body 的一次解释执行。

```scheme
(lambda (y)
                                 (if (eq? x y)
                                     arg
                                     (env y))))
```

因为 body 作为函数体，其中可能存在参数 x，我们在对 body 递归解释执行的时候，不可避免地会遇到该函数的参数 x，从而对参数 x 进行解释执行，根据模式匹配的第一条子句，匹配发现参数 x 是一个符号，进而会在环境中查找 x 所对应的值。又由于函数定义式中

```scheme
(lambda (x) body)
```

的参数 x **<u>只</u>**可能在函数应用时才会绑定 bind 相应的值，即

```scheme
((lambda (x) body) n)
```

时才会将 x 的值绑定为 n

所以此时对 body 进行的递归解释执行是不会返回结果的，或者报错，具体情况看 env 函数的具体实现。

那么怎样才能从环境中获取到 x 的值呢？上式中的环境代码的含义是什么呢？我们先看模式匹配的第三条子句。

```scheme
(pmatch expr
            [(,rator ,rand)           
             ((eval-expr rator env)
              (eval-expr rand env))])
```

如果表达式 expr 是形如

```scheme
(rator rand)
```

的函数调用，那么在当前环境里对 rator 进行递归的解释执行，对rand 进行递归的解释执行。

从模式匹配的第二条子句我们得知，对 rator 解释执行的结果会是一个 lambda函数定义式，

于是上式的解释执行转化为

将执行 rator 所得到的定义式应用到对 rand 进行解释执行的值上，此时我们不关心执行rand的值到底是什么。

将执行 rand 的值 （我们叫它 arg），代入到执行 rator 得到的函数定义式里，arg 便对应意味着上边一直强调的 x

所以我们需要将 x 和 arg 作为 kv 键值对添加到此时的环境里，以便于 (env x) 能返回 arg

那么我们是怎么做的呢？

```scheme
(pmatch expr
        [(lambda (,x) ,body)      
             (lambda (arg)
               (eval-expr body (lambda (y)
                                 (if (eq? x y)
                                     arg
                                     (env y)))))])
```

我们观察 body 后的 env 函数实现，只有当 y 和 x 相等时 返回 arg 。y 是什么意思呢？我们在解释执行body的时候，会遇到未绑定的参数 x 当然也会遇到其他的参数，我们只有当该参数是 x 的时候才会返回 arg，其余的情况下依然是执行 (env y)，<u>而且这里的 env 也保证了 我们解释器是词法作用域实现</u>。

建议上边的 env 实现多写几遍，不太好理解。

---

只用三条基本规则，我们就已经写出了一个相当强大的 lisp 解释器原型。

我们在此之上补充几条简单的判断逻辑和四则运算，来看一看代码的运行效果

```scheme
; interp.scm
(define eval-expr
  (lambda (expr env)
    (pmatch expr
      [,n (guard (number? n))
          n]
      [,x (guard (symbol? x))
       (env x)]
      [(zero? ,e)
       (zero? (eval-expr e env))]
      [(sub1 ,e)
       (sub1 (eval-expr e env))]
      [(* ,e1 ,e2)
       (* (eval-expr e1 env)
          (eval-expr e2 env))]
      [(if ,t ,c ,a)
       (if (eval-expr t env)
           (eval-expr c env)
           (eval-expr a env))]
      [(lambda (,x) ,body)
       (lambda (arg)
         (eval-expr body (lambda (y)
                           (if (eq? x y)
                               arg
                               (env y)))))]
      [(,rator ,rand)
       ((eval-expr rator env)
        (eval-expr rand env))])))
(define environment
    (lambda (x) (error 'lookup "oops")))
```





#### 运行

```scheme
> (eval-expr '(((lambda (fact) (lambda (n) ((fact fact) n)))
                (lambda (fact)
                  (lambda (n)
                    (if (zero? n)
                        1
                        (* n ((fact fact) (sub1 n)))))))
               5) environment)

> 120
```



上边的测试用例是阶乘函数。因为没有 define ，所以采用了 [Y 组合子](https://zh.wikipedia.org/wiki/不动点组合子) 的阶乘写法。